include "src/hardware.inc"
include "src/global.inc"

SECTION "main", ROM0

COINCELS_TILE = $23
main::
  xor a
  ldh [rIF],a
  ldh [nmis],a

  ; During the copyright screen (once I get it in), we'll have
  ; up to 4 seconds to run tests.
  xor a
  call run_tests_in_stage_a
  ld a, 1
  call run_tests_in_stage_a

  ; Load title screen
  call lcd_off
  call clear_scrn0_to_0
  ld de, logo_iur
  ld hl, CHRRAM2
  call unpack_iu_file
  ld hl, SCRN_TMP
  ldxy de, 0, 2
  lb bc, 20, 3
  call load_nam

  ld hl, coincels_2b
  ld de, CHRRAM2+COINCELS_TILE*$10
  ld bc, 5*$10
  call memcpy

  ld a,IEF_VBLANK
  ldh [rIE],a  ; enable IRQs
  ld a, %11100100
  ld [rBGP], a
  ld a, LCDCF_ON|LCDCF_BGON|LCDCF_BG8800|LCDCF_BG9800
  ldh [rLCDC], a
  ei
.waitloop:
  rst wait_vblank_irq

  ; 0, 1, 2, 3, 4, 3, 2, 1... 
  ldh a, [nmis]
  rra
  rra
  rra
  and $07
  cp 5
  jr c, .norev
    cpl  ; ff, fe, fd, fc, fb, fa, f9, f8
    sub $FA-3
  .norev:
  add COINCELS_TILE
  ld [$982B], a

  call read_pad
  ldh a, [new_keys]
  and PADF_A|PADF_START
  jr z, .waitloop

  call lcd_off
forever:
  halt
  jr forever







logo_iur: incbin "obj/gb/logo.iur"
coincels_2b: incbin "obj/gb/coincels.2b"
