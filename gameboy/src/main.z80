include "src/hardware.inc"
include "src/global.inc"

SECTION "main", ROM0

DEF COINCELS_TILE equ $23
DEF LOGO_IDOT_XY equ $982B
DEF SHOW_TITLE_SCREEN equ 0

main::
  xor a
  ldh [rIF],a
  ldh [nmis],a


  if WITH_DEBUGHEX
  ld hl, debughex
  ld [hl], $C0
  inc hl
  ld [hl], $DE
  endc

  ; During the copyright screen (once I get it in), we'll have
  ; up to 4 seconds to run tests.
  xor a
  call run_tests_in_stage_a
  ld a, 1
  call run_tests_in_stage_a
  ld a, 2
  call run_tests_in_stage_a
  ld a, 3
  call run_tests_in_stage_a
  ld a, 4
  call run_tests_in_stage_a

  call log_coins_to_sram

  ld hl, $2000
  ld [hl], bank(sgb_send_main_border)
  call sgb_send_main_border
  call lcd_on_blank
  call sgb_unfreeze

  if SHOW_TITLE_SCREEN

  ; Load title screen
  call lcd_off
  call clear_scrn0_to_0
  ld hl, CHRRAM2
  call load_logo

  ; Load the spinning coin
  ld hl, coincels_2b
  ld de, CHRRAM2+COINCELS_TILE*$10
  ld bc, 5*$10
  call memcpy

  ld hl, titlelabels
  ld de, $880
  call vwfDrawLabels

  ld a,IEF_VBLANK
  ldh [rIE],a  ; enable IRQs
  ld a, %01101100
  ldh [rBGP], a
  ld a, LCDCF_ON|LCDCF_BGON|LCDCF_BG8800|LCDCF_BG9800
  ldh [vblank_lcdc_value], a
  ldh [rLCDC], a
  ei
.waitloop:
  rst wait_vblank_irq

  ld b, COINCELS_TILE
  ld hl, LOGO_IDOT_XY
  call logo_cycle_spinning_coin

  call read_pad
  ldh a, [hNewKeys]
  and PADF_A|PADF_START
  jr z, .waitloop

  endc

  jp show_stages

;;
; @param HL place to put logo tiles: $8000 or $9000
load_logo::
  ld de, logo_iur
  call unpack_iu_file
  ld hl, SCRN_TMP
  ldxy de, 0, 2
  lb bc, 20, 3
  jp load_nam

logo_cycle_spinning_coin:
  ; 0, 1, 2, 3, 4, 3, 2, 1... 
  ldh a, [nmis]
  rra
  rra
  rra
  and $07
  cp 5
  jr c, .norev
    cpl  ; ff, fe, fd, fc, fb, fa, f9, f8
    sub $FA-3
  .norev:
  add b
  ld [hl], a
  ret

logo_iur: incbin "obj/gb/logo.iur"
coincels_2b: incbin "obj/gb/coincels.2b"
titlelabels:
  db 8, 56, "I'm at the platform game", 10
  db 8, 64, "I'm at the emu test", 10
  db 8, 72, "I'm at the combination", 10
  db 8, 80, "platform game and emu test", 10
  db 8, 128, $7F, " 2023 Damian Yerrick", 0
