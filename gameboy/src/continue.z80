include "src/hardware.inc"
include "src/global.inc"

; Continue is a conventional test framework to run all tests one by
; one.  It's called "Continue" because in the game's gimmick, it
; represents having collected all coins in stages with 10 of 10
; passes.

COINS_PER_STAGE equ 10
NUM_STAGES equ 10

section "passbits", WRAM0, ALIGN[2]
passbits:: ds NUM_STAGES*2

section "continue_run_tests", ROM0

run_tests_in_stage_a::
  ld b, a

  ; DE: pointer into passbits
  add a
  add low(passbits+1)
  ld e, a
  adc high(passbits+1)
  sub e
  ld d, a

  ; Ring counter for completed tests
  ; When this doubles COINS_PER_STAGE times, carry becomes set
  ; and this stage is complete.
  xor a
  ld [de], a
  dec de
  ld a, $10000 >> COINS_PER_STAGE
  ld [de], a

  ; HL: pointer into coin_list
  ld a, b
  add a
  add a
  add b
  add a
  add a  ; 20 bytes of coin pointers per stage
  add low(coin_list)
  ld l, a
  adc high(coin_list)
  sub l
  ld h, a

  .coinloop:
    ; Save state, run a test, and restore state
    push de
    push hl
    call run_test_hl
    pop hl
    inc hl
    inc hl
    pop de

    ; Record test result (0: pass; 1: fail)
    ld a, [de]
    adc a
    ld [de], a
    inc de
    ld a, [de]
    adc a
    ld [de], a
    dec de
    jr nc, .coinloop
  ret

;;
; Set up the test environment, then run the test whose function
; pointer is at [hl].
run_test_hl:
  ld a, [hl+]
  ld h, [hl]
  ld l, a
  push hl
  ; Now HL is free to set up the test environment, and ret will
  ; start the test


  ret  ; The test begins
